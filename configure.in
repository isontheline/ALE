#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.60)
AC_INIT(ALE, 0.8.5-prerelease, ale@ventricle.dyndns.org)
AC_CONFIG_SRCDIR([ale_accum.h])
AC_CONFIG_HEADER([config.h])

AM_INIT_AUTOMAKE

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC

# The strtod check breaks cross-compilation, so disable it.
AC_DEFUN([AC_FUNC_STRTOD],[])

#GNULib startup
gl_EARLY

# Libtool
AC_PROG_LIBTOOL

# For gnulib.
#gl_SOURCE_BASE(gl)
#gl_M4_BASE(m4)
#gl_LIB(lib)
gl_MODULES(strndup)
gl_INIT

# Checks for libraries.
# FIXME: Replace `main' with a function in `-lm':
AC_CHECK_LIB([m], [main])

# Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([limits.h stdint.h stdlib.h string.h sys/ioctl.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_INT16_T
AC_TYPE_SIZE_T
AC_TYPE_UINT16_T

# Checks for library functions.  (Commented calls cause problems for cross-compilation.)
AC_FUNC_ERROR_AT_LINE
AC_FUNC_FORK
AC_PROG_GCC_TRADITIONAL
# AC_FUNC_MALLOC
# AC_FUNC_REALLOC
AC_FUNC_SELECT_ARGTYPES
AC_FUNC_STRTOD
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([floor memset pow select sqrt strchr strcspn strdup strndup strspn strtol])

# Determine whether assertions should be enabled.
AC_ARG_ENABLE(assertions, [  --enable-assertions          check runtime assertions (default=no)])
if test x$enable_debug = xyes; then
	ASSERT_FLAGS="-DDEBUG"
else
	ASSERT_FLAGS="-DNDEBUG"
fi

# Determine whether ImageMagick should be linked
AC_ARG_ENABLE(imagemagick, [  --enable-imagemagick         imagemagick file handling: auto,no,yes (default=auto)])
if test x$enable_imagemagick != xno; then

	# AC_CHECK_TOOL is somewhat broken for cross-compilation in
	# some versions of autoconf, so use a conditional instead.
	
	if test x$host = x$build \
	|| test x$host = x; then 
		AC_CHECK_TOOL(MAGICK_CONFIG, Magick-config, no)
	else
		AC_CHECK_PROG(MAGICK_CONFIG, ${ac_tool_prefix}Magick-config, ${ac_tool_prefix}Magick-config, no)
	fi

	if test x$MAGICK_CONFIG != xno; then

		# Bugs have been encountered in certain versions of ImageMagick
		# older than 6.0.6.  Until a test is written to check for these
		# bugs, check the version number instead.

		IMAGEMAGICK_VERSION=`Magick-config --version`
		IMAGEMAGICK_VERSION_A=`echo $IMAGEMAGICK_VERSION | cut -f 1 -d '.'`
		IMAGEMAGICK_VERSION_B=`echo $IMAGEMAGICK_VERSION | cut -f 2 -d '.'`
		IMAGEMAGICK_VERSION_C=`echo $IMAGEMAGICK_VERSION | cut -f 3 -d '.'`

		if test $IMAGEMAGICK_VERSION_A -gt 6 \
		|| ( test $IMAGEMAGICK_VERSION_A -eq 6 \
		  && test $IMAGEMAGICK_VERSION_B -gt 0 ) \
		|| ( test $IMAGEMAGICK_VERSION_A -eq 6 \
		  && test $IMAGEMAGICK_VERSION_B -eq 0 \
		  && test $IMAGEMAGICK_VERSION_C -ge 6 ); then
		  	IMAGEMAGICK_CFLAGS=`$MAGICK_CONFIG --cflags`
			IMAGEMAGICK_CPPFLAGS="`$MAGICK_CONFIG --cppflags` -DUSE_MAGICK"
			IMAGEMAGICK_LDFLAGS=`$MAGICK_CONFIG --ldflags`
			IMAGEMAGICK_LIBS=`$MAGICK_CONFIG --libs`
		else
			AC_MSG_WARN([not using ImageMagick older than 6.0.6 (found $IMAGEMAGICK_VERSION)])
		fi
	fi
else
	IMAGEMAGICK_CFLAGS=""
	IMAGEMAGICK_CPPFLAGS=""
	IMAGEMAGICK_LDFLAGS=""
	IMAGEMAGICK_LIBS=""
fi

# Fatal error if user flag for ImageMagick could not be satisified
if test x$enable_imagemagick = xyes \
&& test x$IMAGEMAGICK_CPPFLAGS = x; then
	AC_MSG_ERROR([ImageMagick build requested (--enable-imagemagick=yes), but no suitable version was found.])
fi

# Determine whether FFTW should be linked
AC_ARG_ENABLE(fftw3, [  --enable-fftw3               Use FFTW3 for fourier transforms: auto,no,yes (default=auto)])
if test x$enable_fftw3 != xno; then
	AC_CHECK_LIB([fftw3], [main], LIBS="-lfftw3 $LIBS" FFTW_CPPFLAGS="-DUSE_FFTW")
fi

# Fatal error if user flag for FFTW could not be satisified
if test x$enable_fftw3 = xyes \
&& test x$FFTW_CPPFLAGS = x; then
	AC_MSG_ERROR([FFTW3 build requested (--enable-fftw3=yes), but no suitable version was found.])
fi

# Check for Unix facilities.
AC_CHECK_HEADER(unistd.h, AC_DEFINE([HAVE_UNISTD_H]))
AC_CHECK_FUNC(execlp, AC_DEFINE([HAVE_EXECLP], [], [execlp() function is available.]))

if test x$ac_cv_func_fork_works = xyes \
&& test x$ac_cv_header_unistd_h = xyes \
&& test x$ac_cv_func_execlp = xyes; then
	UNIX_CPPFLAGS="-DUSE_UNIX"
fi

OPTION_CPPFLAGS="$ASSERT_FLAGS $IMAGEMAGICK_CPPFLAGS $FFTW_CPPFLAGS $UNIX_CPPFLAGS"
OPTION_CFLAGS="$IMAGEMAGICK_CFLAGS"
OPTION_LDFLAGS="$IMAGEMAGICK_LDFLAGS"
OPTION_LIBS="$IMAGEMAGICK_LIBS"

AC_SUBST(OPTION_CPPFLAGS)
AC_SUBST(OPTION_CFLAGS)
AC_SUBST(OPTION_LDFLAGS)
AC_SUBST(OPTION_LIBS)

AC_CONFIG_FILES([Makefile lib/Makefile])

AC_OUTPUT

# Since the generated Makefile seems to sometimes miss build steps that are
# required due to reconfiguration, 'make clean' here.

AC_MSG_NOTICE(making clean)
make clean &> /dev/null

